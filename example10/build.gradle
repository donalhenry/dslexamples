buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath 'org.ajoberstar.grgit:grgit-gradle:3.1.1'
  }
}

apply plugin: 'base'

task getRepo (type: GitGetTask) {
  repoName 'dslexamples'
  baseUrl 'https://github.com/donalhenry'
}

clean.dependsOn cleanGetRepo

task zipRepo (type: ZipRepo) {
  from getRepo
  version getRepo.version
}

import org.ajoberstar.grgit.Grgit

class GitGetTask extends DefaultTask {
  @Input String repoName
  @Input String baseUrl
  @Input String branchName = 'master'
  @OutputDirectory File getDestinationDir() {
    project.file("$project.buildDir/$repoName")
  }

  Provider<String> getVersion() {
    project.provider {
      def gitRepo = Grgit.open(dir: destinationDir)
      gitRepo.head().abbreviatedId
    }
  }

  @TaskAction doIt() {
    def gitUrl = "$baseUrl/$repoName"
    println "Checking out $gitUrl(origin/$branchName)..."
    Grgit.clone(dir: destinationDir, uri: gitUrl, checkout: true, refToCheckout: branchName)
  }
}

class ZipRepo extends Zip {
  final PropertyState<String> versionZip = project.objects.property(String)

  String getVersion() {
    this.versionZip.get()
  }

  void setVersion(Provider<String> versionZip) {
    this.versionZip.set(versionZip)
  }
}
